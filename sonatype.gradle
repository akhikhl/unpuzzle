/* This script deals with signing archives and uploading to sonatype repository.
 * It is project-specific and it is applied to every subproject.
 * To publish to sonatype repository, you need to do the following:
 * 1. Create GPG signing key, add it to your public key ring.
 * 2. Publish public GPG key to public keyserver, for example, to keyserver.ubuntu.com
 * 3. Create file "gradle.properties" in ~/.gradle, insert the following content to it:
 *    signing.keyId={gpg-key-id}
      signing.password={gpg-key-passphrase}
      signing.secretKeyRingFile=/home/yourUserName/.gnupg/secring.gpg
      sonatypeUsername={sonatype-username}
      sonatypePassword={sonatype-password}
      (Please substitute real data instead of curly braces).
   4. Run from command line:
      gradle uploadArchives -PpublishToSonatype=true
 */

apply plugin: 'signing'

afterEvaluate {
  if((plugins.findPlugin('java') || plugins.findPlugin('groovy')) &&
    project.hasProperty('publishToSonatype') &&
    project.hasProperty('signing.keyId') && 
    project.hasProperty('signing.password') && 
    project.hasProperty('signing.secretKeyRingFile') && 
    project.hasProperty('sonatypeUsername') && 
    project.hasProperty('sonatypePassword')) {
    
    signing {
      sign configurations.archives
    }

    uploadArchives {
      repositories {
        mavenDeployer {
          beforeDeployment { deployment -> signing.signPom(deployment) }

          repository(url: 'https://oss.sonatype.org/service/local/staging/deploy/maven2/') {
            authentication(userName: project.sonatypeUsername, password: project.sonatypePassword)
          }

          pom.project {
            name project.name
            packaging 'jar'
            description 'Tools for mavenizing OSGI bundles'
            url 'https://github.com/akhikhl/unpuzzle'

            scm {
              url 'scm:https://github.com/akhikhl/unpuzzle.git'
              connection 'scm:https://github.com/akhikhl/unpuzzle.git'
              developerConnection 'scm:git@github.com:akhikhl/unpuzzle.git'
            }

            licenses {
              license {
                name 'The MIT License'
                url 'https://raw.github.com/akhikhl/unpuzzle/master/license.txt'
                distribution 'repo'
              }
            }

            developers {
              developer {
                id 'akhikhl'
                name 'Andrey Hihlovskiy'
              }
            }
          }
        }
      }
    } // uploadArchives
  }   
}
